group 'org.openrndr.template'
version '1.0-SNAPSHOT'

buildscript {
    ext.kotlin_version = '1.2.31'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'application'

sourceCompatibility = 1.8
project.ext.openrndrVersion = "0.3.5"

repositories {
    mavenCentral()
    maven {
        url="https://dl.bintray.com/openrndr/openrndr/"
    }
}

switch (org.gradle.internal.os.OperatingSystem.current()) {
    case org.gradle.internal.os.OperatingSystem.WINDOWS:
        project.ext.openrndrOS = "windows"
        break
    case org.gradle.internal.os.OperatingSystem.LINUX:
        project.ext.openrndrOS = "linux"
        break
    case org.gradle.internal.os.OperatingSystem.MAC_OS:
        project.ext.openrndrOS = "macos"
        break
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    testCompile group: 'junit', name: 'junit', version: '4.12'
    runtime "org.openrndr:openrndr-gl3:$openrndrVersion"
    runtime "org.openrndr:openrndr-gl3-natives-$openrndrOS:$openrndrVersion"
    compile "org.openrndr:openrndr-core:$openrndrVersion"
    compile "org.openrndr:openrndr-svg:$openrndrVersion"
    compile "org.openrndr:openrndr-animatable:$openrndrVersion"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

mainClassName = "TemplateProgramKt"
applicationDefaultJvmArgs = ["-XstartOnFirstThread"]

ext.mainClassName = mainClassName
jar {
    manifest {
        attributes 'Main-Class': mainClassName
    }
    doFirst {
        from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
        from { configurations.runtime.collect { it.isDirectory() ? it : zipTree(it) } }
    }
    exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA', '**/module-info*'
}

